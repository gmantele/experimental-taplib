apply plugin: 'java'
apply plugin: 'eclipse'

defaultTasks = ["check"]


/* *************************************************************************
 * * PROJECT                                                               *
 * ************************************************************************* */

description = "UWS Service Framework."

archivesBaseName = 'uwslib'
javadoc.title = archivesBaseName

version = '4.1'
project.ext.implVersion = version+'.0'


/* *************************************************************************
 * * SOURCES                                                               *
 * ************************************************************************* */

sourceCompatibility = 1.7

sourceSets.main.java.srcDirs = ['src']
sourceSets.test.java.srcDirs = ['test']


/* *************************************************************************
 * * COMPILATION                                                           *
 * ************************************************************************* */

compileJava.options.encoding = 'UTF-8'


/* *************************************************************************
 * * DEPENDENCIES                                                          *
 * ************************************************************************* */

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'javax.servlet:javax.servlet-api:3.0.1'
    compile 'org.json:json:20160810'
    compile fileTree(dir: 'lib', include: 'cos-1.5beta.jar')
    
    testCompile 'junit:junit:4.12'
}


/* *************************************************************************
 * * JAR GENERATION                                                        *
 * ************************************************************************* */

// NOTE: Specification  = API
//       Implementation = implementation of the API (for instance, when bugs
//                        are fixed, the implementation changes but not the
//                        API).
jar.manifest {
    attributes 'Implementation-Title': 'uws',
               'Implementation-Version': project.ext.implVersion,
               'Implementation-Vendor': 'Gregory Mantelet',
               'Specification-Title': 'UWSLib',
               'Specification-Version': version,
               'Specification-Vendor': 'Gregory Mantelet',
               'Class-Path': configurations.runtime
                              .collect { it.getName() }.join(' ')
}

// All the JUnit tests MUST be passed in order to build and
// distribute a JAR of this library.
jar.dependsOn('test')


/* *************************************************************************
 * * CUSTOM TASKS                                                          *
 * ************************************************************************* */

// DEPENDENCIES FETCHING TASKS
// (only useful for the ANT script `build.xml`.

task fetchDependencies{
    description "Fetches all runtime and test dependencies in resp. 'lib' " +
                "and 'test-lib'. This task is useful only the compilation " +
                "using the ANT script 'build.xml'."

    dependsOn 'fetchRuntimeDependencies,' +
              'fetchTestDependencies'
}

task fetchRuntimeDependencies(type: Copy) {
    description "Fetches all runtime dependencies in 'lib'."
    
    from configurations.runtime
    into "lib"
}

task fetchTestDependencies(type: Copy) {
    description "Fetches all test dependencies in 'test-lib'."

    from configurations.testRuntime - configurations.runtime
    into "test-lib"
}


/* ########################################################################## */

/*
compileJava {
    options.encoding = "UTF-8"
}

test{
	forkEvery = 1
	include '**\/Test*'
}
*/
